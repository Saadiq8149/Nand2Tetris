// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	//// Replace this comment with your code.
    Mux16(a= instruction, b= aluOut, sel= instruction[15], out= preA);
    Not(in= instruction[15], out= notInsType);
    Or(a= notInsType, b= instruction[5], out= loadaReg);
    ARegister(in= preA, load= loadaReg, out= aReg, out[0..14]=addressM);
    Mux16(a= aReg, b= inM, sel= instruction[12], out= y);
    And(a= instruction[4], b= instruction[15], out= loadReg);
    DRegister(in= aluOut, load= loadReg, out= dReg);
    ALU(x= dReg, y= y, zx= instruction[11], nx= instruction[10], zy= instruction[9], ny= instruction[8], f= instruction[7], no= instruction[6], out= aluOut, out=outM, zr= aluOutZero, ng= aluOutNeg);
    Not(in= aluOutZero, out= NotaluOutZero);
    Not(in= aluOutNeg, out= NotaluOutNeg);
    And(a= NotaluOutZero, b= NotaluOutNeg, out= aluOutPos);
    And(a= instruction[3], b= instruction[15], out= writeM);
    
    And(a= aluOutPos, b= instruction[0], out= jgt);
    And(a= aluOutZero, b= instruction[1], out= jeq);
    And(a= aluOutNeg, b= instruction[2], out= jng);
    Or(a= jgt, b= jeq, out= temp);
    Or(a= temp, b= jng, out= temp1);
    And(a= temp1, b= instruction[15], out= jump);

    PC(in= aReg, load= jump, inc= true, reset= reset, out[0..14]= pc);
}